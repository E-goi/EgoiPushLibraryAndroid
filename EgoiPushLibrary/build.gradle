plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'com.vanniktech.maven.publish'
    id 'signing'
}

android {
    defaultConfig {
        compileSdk 36
        minSdkVersion 23
        targetSdkVersion 36
        multiDexEnabled true

        aarMetadata {
            minCompileSdk 29
        }

        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    buildFeatures {
        viewBinding true
    }

    namespace 'com.egoiapp.egoipushlibrary'
}

mavenPublishing {
    coordinates("com.egoiapp.egoipushlibrary", "egoipushlibrary", "4.2.0")
    
    pom {
        name = "EgoiPushLibrary"
        description = "Android library responsible for handling notifications from E-goi."
        inceptionYear = "2021"
        url = "https://github.com/E-goi/EgoiPushLibraryAndroid"
        
        licenses {
            license {
                name = "The Apache Software License, Version 2.0"
                url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                distribution = "http://www.apache.org/licenses/LICENSE-2.0.txt"
            }
        }
        
        developers {
            developer {
                id = "jsilva"
                name = "Jo√£o Silva"
                email = "jsilva@e-goi.com"
            }
        }
        
        scm {
            url = "https://github.com/E-goi/EgoiPushLibraryAndroid"
            connection = "scm:git:git://github.com/E-goi/EgoiPushLibraryAndroid.git"
            developerConnection = "scm:git:ssh://git@github.com/E-goi/EgoiPushLibraryAndroid.git"
        }
    }

    publishToMavenCentral()
    signAllPublications()
}



dependencies {
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.10.2'
    implementation 'androidx.core:core-ktx:1.17.0'
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'androidx.work:work-runtime-ktx:2.10.3'
    implementation 'androidx.datastore:datastore-preferences:1.1.7'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'com.google.android.gms:play-services-location:21.3.0'
    implementation "androidx.multidex:multidex:2.0.1"
}