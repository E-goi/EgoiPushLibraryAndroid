plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
    id 'signing'
}

android {
    defaultConfig {
        compileSdk 34
        minSdkVersion 19
        targetSdkVersion 34

        aarMetadata {
            minCompileSdk 29
        }

        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        viewBinding true
    }

    namespace 'com.egoiapp.egoipushlibrary'

    publishing {
        singleVariant('release') {
            withSourcesJar()
        }
    }
}

publishing {
    publications {
        release(MavenPublication) {
            groupId = 'com.egoiapp.egoipushlibrary'
            artifactId = 'egoipushlibrary'
            version = '4.1.1'

            pom {
                name = "EgoiPushLibrary"
                description = "Android library responsible for handling notifications from E-goi."
                url = "https://github.com/E-goi/EgoiPushLibraryAndroid"
                packaging = 'aar'

                licenses {
                    license {
                        name = "The Apache Software License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }

                developers {
                    developer {
                        id = "jsilva"
                        name = "Jo√£o Silva"
                        email = "jsilva@e-goi.com"
                    }
                }

                scm {
                    connection = "https://github.com/E-goi/EgoiPushLibraryAndroid.git"
                    developerConnection = "https://github.com/E-goi/EgoiPushLibraryAndroid.git"
                    url = "https://github.com/E-goi/EgoiPushLibraryAndroid"
                }
            }

            afterEvaluate {
                from components.release
            }
        }
    }

    repositories {
        maven {
            url = uri('https://oss.sonatype.org/service/local/staging/deploy/maven2/')
            credentials {
                username "${ossrhUsername}"
                password "${ossrhPassword}"
            }
        }
    }
}

signing {
    sign publishing.publications.release
}

dependencies {
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.7.3'
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.work:work-runtime-ktx:2.8.1'
    implementation 'androidx.datastore:datastore-preferences:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'com.google.android.material:material:1.10.0'
    implementation 'com.google.android.gms:play-services-location:21.0.1'
}