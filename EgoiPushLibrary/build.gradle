plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.5'
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }
}

tasks.withType(Javadoc) {
    enabled = false
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    getArchiveClassifier().set("sources")
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    getArchiveClassifier().set("javadoc")
    from javadoc.destinationDir
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                artifact javadocJar
                artifact sourcesJar

                groupId = "com.egoi"
                artifactId = "egoipushlibrary"
                version = "1.0.11"

                pom {
                    name = "EgoiPushLibrary"
                    description = "Android library responsible for handling notifications from E-goi."
                    url = "https://github.com/E-goi/EgoiPushLibraryAndroid"

                    licenses {
                        license {
                            name = "The Apache Software License, Version 2.0"
                            url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                        }
                    }

                    developers {
                        developer {
                            id = "jsilva"
                            name = "Jo√£o Silva"
                            email = "jsilva@e-goi.com"
                        }
                    }

                    scm {
                        connection = "https://github.com/E-goi/EgoiPushLibraryAndroid.git"
                        developerConnection = "https://github.com/E-goi/EgoiPushLibraryAndroid.git"
                        url = "https://github.com/E-goi/EgoiPushLibraryAndroid"
                    }
                }

                pom.withXml {
                    def dependenciesNode = asNode()['dependencies'][0] ?: asNode().appendNode('dependencies')

                    // Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
                    configurations.implementation.allDependencies.each {
                        // Ensure dependencies such as fileTree are not included.
                        if (it.name != 'unspecified') {
                            def dependencyNode = dependenciesNode.appendNode('dependency')
                            dependencyNode.appendNode('groupId', it.group)
                            dependencyNode.appendNode('artifactId', it.name)
                            dependencyNode.appendNode('version', it.version)
                        }
                    }
                }
            }
        }
    }
}

bintray {
    user = System.getenv("BINTRAY_USER")
    key = System.getenv("BINTRAY_KEY")

    publications = ["release"]

    publish = true
    override = true

    pkg {
        repo = "Maven"
        name = "EgoiPushLibrary"
        userOrg = System.getenv("BINTRAY_USER")
        licenses = ['Apache-2.0']
        websiteUrl = 'https://github.com/E-goi/EgoiPushLibraryAndroid'
        vcsUrl = 'https://github.com/E-goi/EgoiPushLibraryAndroid.git'
        githubRepo = 'E-goi/EgoiPushLibraryAndroid'

        version {
            name = "1.0.11"
            released = new Date()
            vcsTag = "1.0.11"
        }
    }
}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.2"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.work:work-runtime-ktx:2.5.0'
    implementation 'androidx.datastore:datastore-preferences:1.0.0-alpha06'
    implementation 'androidx.localbroadcastmanager:localbroadcastmanager:1.0.0'
    implementation 'com.android.volley:dc-volley:1.1.0'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'com.google.android.gms:play-services-location:17.1.0'
}